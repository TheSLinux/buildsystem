# Maintainer: Duc M. Tran <901stt@gmail.com>
# Original Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Original Contributor: tobias <tobias funnychar archlinux.org>

pkgdesc="A simple client-server configuration storage and query system"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
# http://www.xfce.org/documentation/requirements
# keep dbus, see also http://bugs.archlinux.org/task/14536
depends=('libxfce4util' 'dbus-glib' 'dbus')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig' 'glib-perl'
             'intltool' 'gtk-doc' 'chrpath')
options=('!libtool' '!emptydirs')
source=(http://archive.xfce.org/src/xfce/${pkgbase}/4.10/${pkgbase}-${pkgver}.tar.bz2)

build() {
  cd "$srcdir/${pkgbase}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --enable-gtk-doc \
    --with-perl-options=INSTALLDIRS="vendor" \
    --disable-debug
  make
}

package() {
  cd "$srcdir/${pkgbase}-${pkgver}"

  make DESTDIR="$pkgdir" install

  # remove unneeded dynloader bootstrap file
  rm "$pkgdir/usr/lib/perl5/vendor_perl/auto/Xfce4/Xfconf/Xfconf.bs"

  # fix insecure rpath, http://bugs.archlinux.org/task/19980
  chrpath -d "$pkgdir/usr/lib/perl5/vendor_perl/auto/Xfce4/Xfconf/Xfconf.so"
}
