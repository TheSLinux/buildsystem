# Maintainer: Anh K. Huynh <kyanh@theslinux.org>
# Original Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Original Contributor: kevin <kevin@archlinux.org>

# theslinux version number  => ntp version
#     4.2.6.5                    4.2.6p5
_ntpver="${PACKAGE_VERSION%.*}p${PACKAGE_VERSION##*.}"
_ntpbranch="${PACKAGE_VERSION%.*}"
_ntpbranch="${_ntpbranch%.*}"

pkgdesc='Network Time Protocol reference implementation'
url='http://www.ntp.org/'
license=('custom')
arch=('i686' 'x86_64')
makedepends=('perl-html-parser')
depends=('openssl' 'libcap' 'libedit')
backup=('etc/ntp.conf')
source=("http://www.eecis.udel.edu/~ntp/ntp_spool/ntp4/ntp-${_ntpbranch}/ntp-${_ntpver}.tar.gz"
        'ntp.conf'
        'ntpd.service'
        'ntpdate.service')

install=install

build() {
	cd "${srcdir}/${pkgname}-${_ntpver}"

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-linuxcaps \
		--enable-ntp-signd \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${_ntpver}"

	make DESTDIR="${pkgdir}" install

	rmdir "${pkgdir}"/usr/{lib,sbin}
	install -d -o 87 "${pkgdir}"/var/lib/ntp
	install -Dm644 ../ntp.conf "${pkgdir}"/etc/ntp.conf
	install -Dm644 ../ntpd.service "${pkgdir}"/usr/lib/systemd/system/ntpd.service
	install -Dm644 ../ntpdate.service "${pkgdir}"/usr/lib/systemd/system/ntpdate.service
	install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	cd html
	../scripts/html2man
	install -d "${pkgdir}"/usr/share/man
	mv man/man* "${pkgdir}"/usr/share/man
	mv "${pkgdir}/usr/share/man/man8/ntpd.8" "${pkgdir}/usr/share/man/man8/ntp-ntpd.8" # we should ditch openntpd

	install -dm755 "${pkgdir}/usr/lib/systemd/ntp-units.d"
	echo 'ntpd.service' > "${pkgdir}/usr/lib/systemd/ntp-units.d/${pkgname}.list"
}
